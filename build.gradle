buildscript {
    ext {
        springBootVersion = '1.3.2.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.github.ksoichiro.web.resource" version "1.2.0"
    id "org.sonarqube" version "1.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'

jar {
    baseName = 'tinkerbell'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework:spring-jdbc")
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-devtools')

    compile('org.springframework.session:spring-session:1.2.0.RC1')
    runtime("mysql:mysql-connector-java")
    compile('nl.jqno.equalsverifier:equalsverifier:2.0.2')
    // https://mvnrepository.com/artifact/joda-time/joda-time
    compile group: 'joda-time', name: 'joda-time', version: '2.9.7'
    compile group: 'com.googlecode.owasp-java-html-sanitizer', name: 'owasp-java-html-sanitizer', version: '20160628.1'

    // https://mvnrepository.com/artifact/de.sven-jacobs/loremipsum
    compile group: 'de.sven-jacobs', name: 'loremipsum', version: '1.0'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('com.h2database:h2')
    testRuntime('com.h2database:h2')
    // https://mvnrepository.com/artifact/org.seleniumhq.selenium/selenium-java
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.2.0'

    compile("org.springframework.boot:spring-boot-starter-websocket")

    //Webjars
    compile("org.webjars:jquery:2.1.4")
    compile("org.webjars:bootstrap:3.3.4")
    compile("org.webjars:ace:1.2.2")
    compile("org.webjars.bower:datatables:1.10.11")
    compile 'org.webjars.npm:slick-carousel:1.5.9'
    compile("org.webjars.bower:google-code-prettify:1.0.4")
    compile("org.webjars.npm:jquery-ui:1.12.0-rc.2")
    compile group: 'org.webjars', name: 'font-awesome', version: '4.7.0'
    compile("org.webjars:webjars-locator")
    compile("org.webjars:sockjs-client:1.0.2")
    compile("org.webjars:stomp-websocket:2.3.3")
    compile "com.heroku.sdk:heroku-jdbc:0.1.1"


    compile fileTree(dir: 'libs', include: ['*.jar'])
    // compile ('redis.clients:jedis:2.8.1')
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

task printSonarProperties {
    println System.getProperty('sonar.projectName')
    println System.getProperty('sonar.projectKey')
}

task zip(type: Zip, dependsOn: bootRepackage) {
    from('.') {
        include 'keystore.p12'
    }
    from('build/libs') {
        include '*.jar'
    }
    from('src/main/resources/static/') {
        into 'static'
    }
    from('src/main/resources/templates/') {
        into 'templates'
    }
    from('sql-resources') {
        into 'sql-resources'
    }
    from('src/main/resources/config/') {
        into 'config'
    }
}
build.dependsOn(zip)

//Documentation: https://github.com/ksoichiro/gradle-web-resource-plugin
webResource {
    lib {
        // Change directories for libraries downloaded with bower
        dest = '/../../../src/main/resources/static/js/lib'
    }
    // (Option) Write dependencies if you want to use library from bower
    bower {
        dependencies {
            install name: 'datatables', version: '1.10.11', filter: ['media/css/*.min.css', 'media/js/*.min.js', 'media/images/*']
            install name: 'jquery.gritter', version: '1.7.4', filter: ['css/*.css', 'js/*.min.js', 'images/*']
        }
    }
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

bootRun{
    systemProperties System.properties
}

